" Initialisation
" {{{

  set nocompatible
  set encoding=utf-8
  scriptencoding utf-8

  let s:vimdir = expand("~") . "/.vim"

  " disable defaults.vim
  let g:skip_defaults_vim = 1

  " set up pathogen
  runtime bundle/pathogen/autoload/pathogen.vim
  if !exists('g:pathogen_disabled')
    let g:pathogen_disabled = []
  endif

  " disable netrw
  let g:loaded_netrwPlugin = 1

  " only enable tmux plugins in tmux sessions
  if empty($TMUX)
    call add(g:pathogen_disabled, 'tmux-complete')
  endif

  " load bundles
  call pathogen#infect()

  augroup vimrc
    autocmd!
  augroup END

" }}}

" General Settings
" {{{

  " file handling
  filetype plugin indent on
  set hidden
  set nobackup
  set writebackup
  set backupcopy=auto
  set modeline
  set modelines=2
  set updatetime=100

  if v:version > 703 && !has('nvim')
    set cryptmethod=blowfish
  endif

  " searching
  set ignorecase
  set smartcase
  set hlsearch
  set incsearch
  if exists('&inccommand')
    set inccommand=nosplit
  endif

  if executable('rg')
    let &grepprg = "rg --vimgrep"
  endif

  " store swapfiles in a central location
  set directory=~/.vim/tmp/swap//,.,/var/tmp//,/tmp//
  if !isdirectory(s:vimdir . '/tmp/swap')
    call mkdir(s:vimdir . '/tmp/swap', 'p')
  endif

  " enable persistent undo
  if has('persistent_undo')
    set undofile
    set undodir=~/.vim/tmp/undo

    if !isdirectory(&undodir)
      call mkdir(&undodir, 'p')
    endif
  endif

  " spell checking
  if has('spell')
    set nospell
    set spellfile=~/.vim/spells/en.add

    if !isdirectory(s:vimdir . '/spells')
      call mkdir(s:vimdir . '/spells', 'p')
    endif
  endif

" }}}

" Display Settings
" {{{

  " interface
  let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
  let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"
  set termguicolors

  syntax on
  colorscheme desertink

  " colorscheme PaperColor
  " hi! link Folded StatusLineNC
  " hi! LineNr guibg=#121212
  " hi! clear Visual
  " hi! Visual guibg=#005F87
  " hi! VisualNOS guibg=#004866

  " terminals
  if has('nvim')
    autocmd vimrc TermOpen * setlocal nonumber norelativenumber signcolumn=no | startinsert!
    autocmd vimrc BufWinEnter,WinEnter term://* startinsert!
    autocmd vimrc BufWinLeave,WinLeave term://* stopinsert
  endif

  " ambience theme from terminator
  if has('nvim')
    let g:terminal_color_0  = '#2e3436'
    let g:terminal_color_1  = '#cc0000'
    let g:terminal_color_2  = '#4e9a06'
    let g:terminal_color_3  = '#c4a000'
    let g:terminal_color_4  = '#3465a4'
    let g:terminal_color_5  = '#75507b'
    let g:terminal_color_6  = '#06989a'
    let g:terminal_color_7  = '#d3d7cf'
    let g:terminal_color_8  = '#555753'
    let g:terminal_color_9  = '#ef2929'
    let g:terminal_color_10 = '#8ae234'
    let g:terminal_color_11 = '#fce94f'
    let g:terminal_color_12 = '#729fcf'
    let g:terminal_color_13 = '#ad7fa8'
    let g:terminal_color_14 = '#34e2e2'
    let g:terminal_color_15 = '#eeeeec'
  endif

  set errorbells
  set visualbell t_vb= " disable bells
  set cmdheight=1
  set signcolumn=yes
  set synmaxcol=1000 " disable syntax highlighting on long lines
  set lazyredraw
  if !&diff
    set nocursorline
  endif

  " mouse support
  set mouse=a
  if exists('&ttymouse')
    set ttymouse=xterm2
  endif

  " increase command-line height during startup to avoid prompt on long filenames
  " set cmdheight=2
  " autocmd vimrc VimEnter * set cmdheight=1

  " windows
  set winheight=3
  set winminheight=3
  set splitbelow
  set splitright

  " auto-completion
  set completeopt=menuone,noinsert
  set shortmess+=c

  " line numbers
  set number
  set numberwidth=6
  set norelativenumber

  " scrolling
  set scrolloff=5
  set sidescrolloff=8
  set sidescroll=1
  set virtualedit+=block
  set nostartofline
  set display+=lastline

  if has('conceal')
    set concealcursor=nvc
    set conceallevel=0
  endif

  " command prompt and status line
  set showcmd
  set ruler
  set laststatus=2
  set history=1000
  set report=0
  set wildmenu
  set wildmode=list:longest,full
  set wildignore=*.pyc,*.o,*.lo,*.la,*.exe,*.swp,*.db,*.bak,*.old,*.dat,*.,tmp,*.mdb,*~,~*

  " automatically open quickfix window after commands that modify the quickfix list
  autocmd vimrc QuickFixCmdPost *grep* if len(getqflist()) | copen | endif

  " highlight trailing whitespace
  autocmd vimrc VimEnter,Colorscheme * hi! Whitespace ctermbg=235 guibg=#333333
  autocmd vimrc VimEnter,Colorscheme * match Whitespace /[^ \t]\s\+$/

  " go to last cursor position when opening files
  autocmd vimrc BufReadPost * if line("'\"") > 0 && line("'\"") <= line("$") && &ft != 'gitcommit'
                                \| exe "normal g'\"" | endif

  " window/icon titles
  set title
  set icon

  if match($TERM, '^\(tmux\|screen\)-') == 0
    let &t_ts = "\033k"
    let &t_fs = "\033\\"

    let &t_IS = "\033]0;"
    let &t_IE = "\007"
  endif

  " show the project name and relative path to the current file
  function! ProjectPath(short) " {{{
    let l:root = getcwd()

    if exists('b:git_dir')
      let l:project = fnamemodify(l:root, ':t')
      let l:path = fnamemodify(expand('%'), ':p:h')

      if a:short || winwidth(0) < 60
        " only return project name
        return l:project
      else
        " return project name with relative path
        return l:project . l:path[strlen(l:root):]
      endif
    else
      return substitute(l:root, '^' . $HOME, '~', '')
    endif
  endfunction " }}}

  let &titlestring = "%t%m%r\ ‚ù∞%{ProjectPath(1)}‚ù±"
  let &iconstring = &titlestring

  " add Unicode marker for tmux navigation
  if !empty($TMUX) || !empty($SUDO_COMMAND)
    let &iconstring .= " %{mode(1) == 'n' || mode() == 't' || &ft == 'fzf' ? 'üòë' : 'üòÆ'}"

    if has('nvim') || !empty($CYGWIN)
      let &titlestring = &iconstring
    endif
  endif

" }}}

" Text Formatting
" {{{

  set formatoptions+=j
  set backspace=indent,eol,start
  set whichwrap+=<,>,[,],h,l
  set iskeyword+=$
  set selection=inclusive
  set nrformats=hex

  " indents
  set autoindent

  " tabs
  set ts=2 sts=2 sw=2
  set expandtab
  set smarttab
  set shiftround

  " matching
  set matchpairs=(:),{:},[:],<:>
  set showmatch
  runtime macros/matchit.vim
  let b:match_ignorecase = 1

  " diffing
  "set diffopt+=iwhite
  set diffopt+=algorithm:patience

  " folding
  set foldenable foldmethod=marker
  autocmd vimrc Syntax * set foldmethod=marker

" }}}

" Filetype Settings
" {{{

  autocmd vimrc FileType c,cs,cpp,php setlocal ts=3 sts=3 sw=3
  autocmd vimrc FileType python,apache,sshconfig setlocal ts=4 sts=4 sw=4
  autocmd vimrc FileType css,scss setlocal iskeyword+=%
  autocmd vimrc FileType make setlocal noexpandtab
  autocmd vimrc FileType text,markdown,mail setlocal linebreak spell suffixesadd+=.md
  autocmd vimrc FileType tex,plaintex,context setlocal lbr
  autocmd vimrc FileType gitcommit,gitrebase setlocal colorcolumn=50,72
  autocmd vimrc FileType crontab setlocal nobackup nowritebackup
  autocmd vimrc FileType puppet setlocal commentstring=#%s
  autocmd vimrc FileType help setlocal nonumber norelativenumber
  autocmd vimrc FileType help nnoremap <buffer><silent> q :bd<CR>
  autocmd vimrc FileType org setlocal foldmethod=expr

  autocmd vimrc BufRead,BufNewFile *.es6 setlocal ft=javascript
  autocmd vimrc BufRead,BufNewFile *.axlsx,*.prawn setlocal ft=ruby

  " Ruby syntax highlighting
  let g:ruby_operators = 1
  let g:ruby_no_expensive = 1

  " Ruby auto-completion
  " let g:rubycomplete_rails = 1
  " let g:rubycomplete_buffer_loading = 1

  " PHP syntax highlighting
  let php_htmlInStrings = 1
  let php_baselib = 1
  let php_special_functions = 1
  let php_alt_comparisons = 1
  let php_alt_AssignByReference = 1
  let php_folding = 1

  " JSON shortcuts
  nnoremap ck ^ci"
  nnoremap cv $F"ci"

  " show recent log entries in commit messages
  function! s:EnhanceCommitMessage()
    " avoid line wrapping
    let textwidth = &textwidth
    let &textwidth = 0

    " return if log entries were already added
    normal gg0
    if search('^# Latest commits:$')
      return
    endif

    if !search('^# Please enter the commit message')
      return
    endif

    normal V
    call search('^# Changes to be committed:$')
    normal kkd

    normal O# Latest commits:

    let command = 'git log -n 5 --no-color --no-decorate --pretty=%s'
    for line in split(system(command), '\n')
      execute 'normal o#  - ' . line
    endfor

    normal o#
    normal gg0

    " insert new line for empty commit messages
    if search('^# Latest commits:$', 'n') == 2
      normal O
      startinsert
    endif

    let &textwidth = textwidth
  endfunction
  autocmd vimrc FileType gitcommit call <SID>EnhanceCommitMessage()

" }}}

" Commands
" {{{

  " helper to preserve search and cursor position
  command! -nargs=1 Preserve call <SID>Preserve(<args>)
  function! s:Preserve(command) " {{{
    let search=@/
    let line = line('.')
    let column = col('.')
    execute a:command
    let @/ = search
    call cursor(line, column)
  endfunction " }}}

  command! -nargs=1 PreserveSearch call <SID>PreserveSearch(<args>)
  function! s:PreserveSearch(command) " {{{
    let search=@/
    execute a:command
    let @/ = search
  endfunction " }}}

  " Replace a builtin command using cabbrev
  " http://vim.wikia.com/wiki/Replace_a_builtin_command_using_cabbrev
  command! -nargs=+ CommandAlias call <SID>CommandAlias(<f-args>)
  function! s:CommandAlias(abbreviation, expansion) " {{{
    execute 'cabbrev ' . a:abbreviation . ' <c-r>=getcmdpos() == 1 && getcmdtype() == ":" ? "' . a:expansion . '" : "' . a:abbreviation . '"<CR>'
endfunction " }}}

  CommandAlias E e
  CommandAlias E! e!
  CommandAlias Q Q
  CommandAlias Q! Q!
  CommandAlias QA qa
  CommandAlias Qa qa
  CommandAlias qA qa
  CommandAlias QA! qa!
  CommandAlias Qa! qa!
  CommandAlias qA! qa!
  CommandAlias WQ wq
  CommandAlias Wq wq
  CommandAlias wQ wq
  CommandAlias WQ! wq!
  CommandAlias Wq! wq!
  CommandAlias wQ! wq!

  " diff shortcuts for visual mode
  command! Dg diffget
  command! Dp diffput

  " shortcuts to open files externally and perform online searches
  if has('mac')
    let g:open_command = 'open'
  elseif has('unix')
    let g:open_command = 'xdg-open'
  else
    let g:open_command = 'start'
  endif
  command! -nargs=+ -complete=file_in_path Open call system(g:open_command.' ' . expand("<args>"))

  " shortcuts to open tmux splits and windows
  command! -nargs=* Mux call system('mux <args>')
  command! -nargs=0 Pry      Mux -b pry
  command! -nargs=0 Server   Mux @server
  command! -nargs=0 Watcher  Mux @watcher

  nnoremap <silent> ,` :Mux -b<CR>
  nnoremap <silent> ,~ :Mux -r<CR>

  " open markdown preview
  command! -nargs=0 VMD execute 'silent !vmd % &>/dev/null &' | redraw!

" }}}

" Keybindings
" {{{

  " Spacemacs escape hatch
  nmap <Space>- ,s
  nmap <Space>= ,v
  nmap <Space><Backspace> ,c
  nmap <Space><CR> ,w

  nmap <Space>bb ,b
  nmap <Space>ff ,f
  nmap <Space>pf ,f
  nmap <Space>ph ,f
  nmap <Space>fr ,r
  nnoremap <Space>bn :bnext<CR>
  nnoremap <Space>bp :bprevious<CR>
  nnoremap <Space>bd :bd<CR>
  nnoremap <Space>bR :e<CR>
  nnoremap <Space>fs :write<CR>
  nnoremap <Space>qq :q<CR>
  nnoremap <Space>qz :q<CR>
  nnoremap <Space>qa :quitall<CR>
  nnoremap <Space>wd <C-w>c
  nnoremap <Space>ws <C-w>s
  nnoremap <Space>wv <C-w>v

  noremap H ^
  noremap L $

  " apply macros with Q (disables the default Ex mode shortcut)
  nnoremap Q @q
  vnoremap Q :norm @q<CR>

  " run a macro over a visual selection without stopping at errors
  " https://github.com/stoeffel/.dotfiles/blob/master/vim/visual-at.vim
  xnoremap @ :<C-u>call <SID>ExecuteMacroOverVisualRange()<CR>
  function! s:ExecuteMacroOverVisualRange()
    echo "@".getcmdline()
    execute ":'<,'>normal @".nr2char(getchar())
  endfunction

  " reindent whole file
  nnoremap =+ mzgg=G`z

  " shortcut for desktop clipboard
  noremap + "+

  " don't use exact searches for */#
  noremap * g*
  noremap # g#
  noremap g* *
  noremap g# #

  " break undo chain on new lines
  inoremap <CR> <C-g>u<CR>

  " duplicate the selection
  vnoremap D y`]pgv

  " repeat command for each line in selection
  xnoremap . :normal .<CR>

  " shortcuts for diff
  nnoremap du :diffupdate<CR>

  " map Ctrl+C to Escape
  inoremap <C-c> <Esc>
  autocmd vimrc CmdWinEnter * nnoremap <buffer> <C-c> :

  " indent with Tab/Shift+Tab in visual mode
  xnoremap <Tab> >
  xnoremap <S-Tab> <

  " delete to black hole register with X
  noremap X "_d
  nnoremap XX "_dd

  " keep cursor position when yanking in visual mode
  xnoremap <silent><expr> y "ygv" . mode()

  " reselect visual block after indent
  xnoremap <silent> > >gv
  xnoremap <silent> < <gv

  " select pasted text
  nmap vp `[v`]

  " toggle paste mode with F12
  set pastetoggle=<f12>

  " terminal
  tnoremap <Esc> <C-\><C-n>

  " navigate windows/tmux panes with Ctrl+jkhl
  function! TmuxNavigate(cmd) " {{{
    let nr = winnr()

    execute('wincmd ' . a:cmd)

    if (empty($TMUX))
      return
    elseif (nr != winnr())
      return
    endif

    let pane = str2nr(system('tmux display -p "#{pane_index}"'))
    let panes = str2nr(system('tmux display -p "#{window_panes}"'))

    if (((a:cmd == 'k' || a:cmd == 'h') && nr == 1 && pane == 1) ||
          \ ((a:cmd == 'j' || a:cmd == 'l') && nr == winnr('$') && pane == panes))
      " silent call system('tmux resize-pane -Z')
    else
      let arg = tr(a:cmd, 'hjkl', 'LDUR')
      silent call system('tmux select-pane -' . arg)
    endif
  endfunction " }}}

  nnoremap <silent> <c-h> :call TmuxNavigate('h')<CR>
  nnoremap <silent> <c-j> :call TmuxNavigate('j')<CR>
  nnoremap <silent> <c-k> :call TmuxNavigate('k')<CR>
  nnoremap <silent> <c-l> :call TmuxNavigate('l')<CR>

  tnoremap <silent> <c-h> <C-\><C-n>:call TmuxNavigate('h')<CR>
  tnoremap <silent> <c-j> <C-\><C-n>:call TmuxNavigate('j')<CR>
  tnoremap <silent> <c-k> <C-\><C-n>:call TmuxNavigate('k')<CR>
  tnoremap <silent> <c-l> <C-\><C-n>:call TmuxNavigate('l')<CR>

  " alias Ctrl-v as Ctrl-q
  noremap <C-q> <C-v>
  inoremap <C-q> <C-v>
  cnoremap <C-q> <C-v>

  " Copy & Paste shortcuts
  if has('mac')
    vnoremap <D-c> "+y
    vnoremap <D-x> "+d

    map <D-v>  "+gP
    cmap <D-v> <C-R>+

    exe 'inoremap <script> <D-v>' paste#paste_cmd['i']
    exe 'vnoremap <script> <D-v>' paste#paste_cmd['v']
  else
    vnoremap <C-c> "+ygv<Esc>
    vnoremap <C-x> "+d<Esc>

    noremap <C-v>  "+gP
    cnoremap <C-v> <C-r>+

    exe 'inoremap <script> <C-v>' paste#paste_cmd['i']
    exe 'vnoremap <script> <C-v>' paste#paste_cmd['v']
  endif

  " keep clipboard when leaving Vim
  if exists('$DISPLAY')
    autocmd vimrc VimLeave * call system('xclip -selection clipboard -o | xclip -selection clipboard -i')
  endif

  " ,q and ,e - toggle quickfix window and location list
  nnoremap <silent> ,q :call <SID>ToggleList('Quickfix List', 'c')<CR>
  nnoremap <silent> ,e :call <SID>ToggleList('Location List', 'l')<CR>
  function! s:GetBufferList() " {{{
    redir =>buflist
    silent! ls
    redir END
    return buflist
  endfunction " }}}
  function! s:ToggleList(bufname, pfx) " {{{
    let buflist = <SID>GetBufferList()
    for bufnum in map(filter(split(buflist, '\n'), 'v:val =~ "'.a:bufname.'"'), 'str2nr(matchstr(v:val, "\\d\\+"))')
      if bufwinnr(bufnum) != -1
        exec(a:pfx.'close')
        return
      endif
    endfor

    if a:pfx == 'c' && !len(getqflist())
      echohl Question
      echo "Quickfix list is empty."
      return
    elseif a:pfx == 'l' && !len(getloclist(0))
      echohl Question
      echo "Location list is empty."
      return
    endif

    let winnr = winnr()
    exec(a:pfx.'open')
    if winnr() != winnr
      wincmd p
    endif
  endfunction " }}}

  " resize quickfix windows to their content, or 20 lines maximum
  autocmd vimrc FileType qf,ale-preview call <SID>ResizeWindow(2, 20)
  function! s:ResizeWindow(min, max) " {{{
    nnoremap <buffer><silent> q :bd<CR>
    nnoremap <buffer><silent> ,E :bd<CR>
    let max = min([a:max, &lines / 3])
    execute max([a:min, min([max, line('$') + 1])]).'wincmd _'
  endfunction " }}}

  " Ctrl-c - remove highlighted search and redraw
  nnoremap <silent> <C-c> :nohlsearch<CR>:redraw!<CR>

  " <count>` - switch buffers
  nnoremap <silent><expr> ` v:count ? ":<C-u>b<C-r>=v:count<CR><CR>" : "`"

  " <count>~ - delete buffers
  nnoremap <silent><expr> ~ v:count ? ":<C-u>bd<C-r>=v:count<CR><CR>" : "~"

  " ,n / ,p - next/previous buffer
  nnoremap <silent> ,n :call <SID>SwitchBuffer(1)<CR>
  nnoremap <silent> ,p :call <SID>SwitchBuffer(-1)<CR>
  function! s:SwitchBuffer(delta)
    let buffers = []
    for nr in range(1, bufnr('$'))
      if buflisted(nr) && getbufvar(nr, 'current_syntax') != 'qf'
        call add(buffers, nr)
      end
    endfor

    if len(buffers) == 0
      return
    endif

    let position = index(buffers, bufnr('%')) + a:delta

    if position < 0
      let position = len(buffers) - 1
    elseif position > len(buffers) - 1
      let position = 0
    endif

    execute 'buffer' . buffers[position]
  endfunction

  " ,c - close current window, unless it's the only one
  nnoremap <silent> ,c :call <SID>CloseWindow()<CR>
  function! s:CloseWindow() " {{{
    " get number of visible buffers
    let buffers = len(filter(range(1, bufnr('$')), 'buflisted(v:val)'))

    if winnr('$') > 1
      if bufname('%') == '[Command Line]'
        close
      else
        lclose
        wincmd c
      endif
    endif
  endfunction " }}}

  " ,x - close the current buffer, keeping windows
  nnoremap <silent> ,x :silent lclose<CR>:silent Bdelete<CR>

  " ,X - close the current buffer, not keeping windows
  nnoremap <silent> ,X :silent lclose<CR>:silent bd<CR>

  " ,d - open file in new split
  nmap <silent> ,d <C-w>f

  " ,R - reload vimrc
  nnoremap <silent> ,R :runtime! vimrc gvimrc<CR>:nohlsearch<CR>

  " ,s - split horizontally
  nnoremap <silent> ,s :split<CR>

  " ,v - split vertically
  nnoremap <silent> ,v :vsplit<CR>

  " ,w - write current buffer
  nnoremap <silent> ,w :write<CR>:redraw<CR>

  " ,$ - clear trailing whitespace
  nnoremap <silent> ,$ :call <SID>ClearTrailingWhitespace()<CR>
  function! s:ClearTrailingWhitespace() " {{{
    Preserve 'silent %s/\s\s*$//e'
    echohl Question
    echo "Trailing whitespace cleared"
    echohl none
  endfunction " }}}

  " ,N - toggle line numbers
  nnoremap <silent> ,N :call <SID>ToggleLineNumbers()<CR>
  function! s:ToggleLineNumbers() " {{{
    if &number
      set nonumber norelativenumber
    else
      set number norelativenumber
    endif
  endfunction " }}}

" }}}

" Plugin Settings
" {{{

  " markdown {{{
  let g:vim_markdown_conceal = 0
  let g:vim_markdown_folding_disabled = 1
  let g:vim_markdown_frontmatter = 1
  let g:vim_markdown_strikethrough = 1
  " }}}

  " dispatch {{{
  let g:dispatch_no_maps = 1
  " }}}

  " org-mode {{{
  " silence warning about missing plugin
  command! -nargs=* -range SpeedDatingFormat echo
  " }}}

  " elm {{{
  let g:elm_detailed_complete = 1
  let g:elm_jump_to_error = 0
  " }}}

  " json {{{
  let g:vim_json_syntax_conceal = (&conceallevel > 0)
  let g:vim_json_syntax_concealcursor = &concealcursor
  " }}}

  " devicons {{{
  let g:webdevicons_enable = 0
  let g:webdevicons_enable_airline_tabline = 0
  let g:webdevicons_enable_airline_statusline = 0
  " }}}

  " vista {{{
  nnoremap <silent> ,t :Vista finder vim_lsp<CR>
  nnoremap <silent> ,T :Vista!!<CR>

  let g:vista_sidebar_width = 40
  let g:vista_blink = [1, 100]
  let g:vista_icon_indent = ["‚óè ", "‚ñ∂ "]
  let g:vista_fzf_preview = ['right:hidden:70%']

  " let g:vista_finder_alternative_executives = [ 'ctags' ]
  let g:vista_executive_for = {
    \ 'javascript': 'vim_lsp',
    \ 'typescript': 'vim_lsp',
    \ 'ruby': 'vim_lsp'
  \ }
  " }}}

  " pasta {{{
  let g:pasta_disabled_filetypes = [ 'python', 'coffee', 'markdown', 'yaml', 'slim' ]
  " }}}

  " man {{{
  runtime ftplugin/man.vim
  " }}}

  " rails {{{
  hi! link rubyRailsARMethod Statement
  " }}}

  " javascript-libraries-syntax {{{
  let g:used_javascript_libs = 'jquery,requirejs'
  " }}}

  " test {{{
  let g:test#strategy = 'dispatch'

  nnoremap <silent> ,! :TestFile<CR>
  nnoremap <silent> ,@ :TestNearest<CR>
  " }}}

  " ragtag {{{
  let g:ragtag_global_maps = 1

  imap <C-]> <C-x>/
  autocmd vimrc User Ragtag silent! iunmap <buffer> <C-v>%
  autocmd vimrc User Ragtag silent! iunmap <buffer> <C-v>&
  " }}}

  " rooter {{{
  let g:rooter_change_directory_for_non_project_files = 'current'
  let g:rooter_use_lcd = 1
  let g:rooter_silent_chdir = 1
  let g:rooter_patterns = [
    \ 'elm-package.json',
    \ 'ng-package.json',
    \ 'package.json',
    \ 'Gemfile',
    \ 'mix.exs',
    \ '.git',
    \ '.git/',
  \ ]
  " }}}

  " matchparen {{{
  let g:matchparen_timeout = 50
  let g:matchparen_insert_timeout = 50
  " }}}

  " tabular {{{
  noremap <silent> ,a :Tabular /
  noremap <silent> ,a{ :Tabular /{<CR>
  noremap <silent> ,a} :Tabular /}<CR>
  noremap <silent> ,a[ :Tabular /[<CR>
  noremap <silent> ,a] :Tabular /]<CR>
  noremap <silent> ,a\| :Tabular /\|<CR>
  noremap <silent> ,a> :Tabular /-><CR>
  noremap <silent> ,a= :Tabular /\(\|\|\)\?=\+>\?<CR>
  noremap <silent> ,a, :Tabular /,\zs/l0r1<CR>
  noremap <silent> ,a: :Tabular /[^:]:\s\zs/l0r1<CR>
  " }}}

  " netrw {{{
  let g:netrw_silent = 1
  let g:netrw_ftpmode = 'ascii'
  let g:netrw_ftp_cmd = 'ftp -p'
  let g:netrw_longlist = 1
  let g:netrw_winsize = 40
  let g:netrw_altv = 1
  let g:DrChipTopLvlMenu = 'Plugins.'
  if has('unix') && exists("$DISPLAY")
    let g:netrw_browsex_viewer = 'xdg-open'
  endif
  " }}}

  " ale {{{
  if index(g:pathogen_disabled, 'ale') == -1
    nnoremap <silent> ,E :ALEDetail<CR>

    highlight! link ALEWarning Whitespace
    highlight! ALEVirtualTextWarning guifg=#999999
    highlight! ALEVirtualTextError guifg=#ff4040 gui=bold
    highlight! link ALEVirtualTextInfo ALEVirtualTextWarning

    let g:ale_lint_on_insert_leave = 1
    let g:ale_lint_on_text_changed = 'normal'

    let g:ale_set_balloons = 0
    let g:ale_virtualtext_cursor = 1
    let g:ale_virtualtext_prefix = '‚Üí '

    let g:ale_linters = {
      \ 'ruby': [ 'ruby', 'rubocop' ],
      \ 'javascript': [ 'eslint' ],
      \ 'typescript': [ 'tslint' ]
    \ }

    let g:ale_sh_shellcheck_options = '-e 2086,2155,2164'
  endif
  " }}}

  " lsp {{{
  if index(g:pathogen_disabled, 'lsp') == -1
    let g:lsp_diagnostics_enabled = 0

    " if executable('solargraph')
    "   au User lsp_setup call lsp#register_server({
    "     \ 'name': 'ruby',
    "     \ 'cmd': { server_info -> ['solargraph', 'stdio'] },
    "     \ 'whitelist': ['ruby']
    "   \ })
    " endif

    if executable('typescript-language-server')
      " https://github.com/ryanolsonx/vim-lsp-typescript
      au User lsp_setup call lsp#register_server({
        \ 'name': 'typescript',
        \ 'cmd': {server_info->[&shell, &shellcmdflag, 'typescript-language-server --stdio']},
        \ 'root_uri': {server_info->lsp#utils#path_to_uri(lsp#utils#find_nearest_parent_file_directory(lsp#utils#get_buffer_path(), 'tsconfig.json'))},
        \ 'whitelist': ['typescript', 'typescript.tsx'],
      \ })

      " https://github.com/ryanolsonx/vim-lsp-javascript
      au User lsp_setup call lsp#register_server({
        \ 'name': 'javascript',
        \ 'cmd': {server_info->[&shell, &shellcmdflag, 'typescript-language-server --stdio']},
        \ 'root_uri': {server_info->lsp#utils#path_to_uri(lsp#utils#find_nearest_parent_file_directory(lsp#utils#get_buffer_path(), 'package.json'))},
        \ 'whitelist': ['javascript', 'javascript.jsx'],
      \ })
    endif
  endif
  " }}}

  " deoplete {{{
  if index(g:pathogen_disabled, 'deoplete') == -1
    let g:deoplete#enable_at_startup = 1

    " <Tab> - trigger and accept completion
    inoremap <silent><expr> <Tab>
      \ pumvisible() ? "\<C-y>" :
      \ <SID>check_back_space() ? "\<Tab>" :
      \ deoplete#mappings#manual_complete()

    function! s:check_back_space() abort
      let col = col('.') - 1
      return !col || getline('.')[col - 1]  =~ '\s'
    endfunction

    " <CR> - reject completion and close popup
    inoremap <CR> <C-r>=(pumvisible() ? "<C-e>" : "")<CR><C-g>u<CR>
  endif
  " }}}

  " airline {{{
  if index(g:pathogen_disabled, 'airline') == -1
    let g:airline_theme = 'desertink'
    let g:airline_powerline_fonts = 1

    let g:airline_inactive_collapse = 0
    let g:airline_detect_paste = 0
    let g:airline_detect_iminsert = 0
    let g:airline_detect_spell = 0

    let g:airline_extensions = [ 'ale', 'branch', 'quickfix', 'tabline' ]

    let g:airline#extensions#ale#show_line_numbers = 0
    let g:airline#extensions#ale#warning_symbol = ''
    let g:airline#extensions#ale#error_symbol = ''

    let g:airline#extensions#branch#displayed_head_limit = 16
    let g:airline#extensions#branch#format = 2
    let g:airline#extensions#branch#vcs_checks = []

    let g:airline#extensions#tabline#buffers_label = 'b'
    let g:airline#extensions#tabline#tabs_label = 't'
    let g:airline#extensions#tabline#overflow_marker = ' ‚Ä¶ '
    let g:airline#extensions#tabline#formatter = 'unique_tail'
    let g:airline#extensions#tabline#buffer_nr_show = 1
    let g:airline#extensions#tabline#buffer_min_count = 2
    let g:airline#extensions#tabline#fnamecollapse = 0

    if !exists('g:airline_symbols')
      let g:airline_symbols = {}
    endif

    " add space after readonly symbol
    call airline#init#bootstrap()
    let g:airline_symbols.readonly .= ' '

    let g:airline_mode_map = {
      \ '__' : '#',
      \ 'n'  : 'n',
      \ 'i'  : 'i',
      \ 'R'  : 'r',
      \ 'v'  : 'v',
      \ 'V'  : 'V',
      \ 'c'  : 'c',
      \ "\026" : 'v‚Ä¢b',
      \ 's'  : 's',
      \ 'S'  : 'S',
      \ "\023" : 's‚Ä¢b',
      \ }

    let g:airline#extensions#default#section_truncate_width = {
      \ 'b': 80
    \ }

    " show file directory separately from file name
    call airline#parts#define_raw('file', '%t%m')
    call airline#parts#define_raw('filepath', '%#airline_filepath#‚ù∞%<%{ProjectPath(0)}‚ù±')
    let g:airline_section_c = airline#section#create(['file', g:airline_symbols.space, 'readonly', 'filepath'])

    hi! airline_filepath          term=bold cterm=bold ctermfg=244 ctermbg=235 gui=bold guifg=#999999 guibg=#303030
    hi! airline_filepath_inactive term=bold cterm=bold ctermfg=238 ctermbg=235 gui=bold guifg=#555555 guibg=#303030

    " show current tag
    call airline#parts#define_raw('tag', '%#airline_tag#%{VistaTag()}')
    hi! airline_tag ctermbg=235 guibg=#303030 ctermfg=215 guifg=#ffc266

    function! VistaTag() abort
      return get(b:, 'vista_nearest_method_or_function', '')
    endfunction
    autocmd vimrc BufRead * call vista#RunForNearestMethodOrFunction()

    " switch filetype and format/encoding, and only show
    " file format and encoding if it's not the default
    let g:airline_section_x = airline#section#create([
      \ 'tag',
      \ "%{&ff == 'unix' ? '' : '  ' . WebDevIconsGetFileFormatSymbol()}%{&fenc == 'utf-8' ? '' : '   ' . &fenc}"
    \ ])
    let g:airline_section_y = airline#section#create(['%{WebDevIconsGetFileTypeSymbol()} %{&ft}'])

    " don't show position percentage
    let g:airline_section_z = airline#section#create(['windowswap', 'linenr', ':%v'])
  endif
  " }}}

  " fzf {{{
  if index(g:pathogen_disabled, 'fzf') == -1
    let g:fzf_layout = { 'window': 'enew' }
    let g:fzf_action = { 'ctrl-x': 'split', 'ctrl-v': 'vsplit' }

    nnoremap <silent> ,f :Files<CR>
    nnoremap <silent> ,r :History<CR>
    nnoremap <silent> ,b :Buffers<CR>
    nnoremap <silent> ,l :BLines<CR>
    nnoremap <silent> ,L :Lines<CR>
    " nnoremap <silent> ,t :BTags<CR>

    nnoremap <silent> ,gg :Rg<CR>
    nnoremap <expr>   ,G ':Rg '.expand('<cword>')

    nnoremap <silent> ,gf :GFiles<CR>
    nnoremap <silent> ,gs :GFiles?<CR>
    nnoremap <silent> ,gl :Commits<CR>
    nnoremap <silent> ,gL :BCommits<CR>
    nnoremap <silent> ,gb :Branches<CR>

    nnoremap <silent> ,: :History:<CR>
    nnoremap <silent> ,/ :History/<CR>
    nnoremap <silent> ,? :Maps<CR>

    " hide statusline in bottom window
    autocmd! vimrc FileType fzf
    autocmd  vimrc FileType fzf echo
      \| set noshowmode
      \| execute "if winnr() == winnr('$') | set laststatus=0 | endif"
      \| autocmd vimrc BufLeave <buffer> set laststatus=2 showmode

    " add preview to :Files
    command! -bang -nargs=? -complete=dir Files
      \ call fzf#vim#files(<q-args>, fzf#vim#with_preview({}, 'right:70%:hidden'), <bang>0)

    " add preview to :Rg
    command! -bang -nargs=* Rg
      \ call fzf#vim#grep(
      \   'rg --column --line-number --no-heading --color=always '.shellescape(<q-args>), 1,
      \   <bang>0 ? fzf#vim#with_preview('right:70%')
      \           : fzf#vim#with_preview('right:70%:hidden'),
      \   <bang>0)

    " tweak :GFiles
    command! -bang -nargs=? GFiles
      \ call fzf#vim#gitfiles(
      \   <q-args>, { 'options': [
      \     '--ansi', '--multi', '--nth', '2..,..', '--tiebreak=index', '--prompt', 'GitFiles?> ',
      \     '--preview', 'git diff --color=always HEAD -- {-1} | sed 1,4d | grep . || fzf-preview {-1}',
      \     '--preview-window', 'right:70%'
      \   ] })

    " add :Branches command
    function! s:fzf_branches(line)
      let l:parser = split(a:line)
      let l:branch = l:parser[0]
      if l:branch ==? '*'
        let l:branch = l:parser[1]
      endif
      execute '!git checkout ' . l:branch
    endfunction

    command! -bang -nargs=0 Branches
      \ call fzf#vim#grep(
      \   'git branches', 0, {
      \     'sink': function('s:fzf_branches'),
      \     'options': ['--prompt', 'Branches> ']
      \   }, <bang>0)
  endif
  " }}}

  " signify {{{
  nnoremap <silent> ,D :SignifyToggleHighlight<CR>
  nnoremap <silent> ,F :SignifyFold!<CR>

  let g:signify_vcs_list = ['git']
  " }}}

  " blame-line {{{
  nnoremap <silent> ,B :ToggleBlameLine<CR>

  let g:blameLineVirtualTextHighlight = 'Question'
  let g:blameLineVirtualTextPrefix = '‚Üí '
  " }}}

  " hexokinase {{{
  let g:Hexokinase_ftAutoload = ['css', 'sass', 'scss', 'vim']
  let g:Hexokinase_optInPatterns = ['full_hex', 'triple_hex', 'rgb', 'rgba', 'colour_names']
  " }}}

  " didyoumean {{{
  let g:dym_use_fzf = 1
  " }}}

  " sandwich {{{
  runtime macros/sandwich/keymap/surround.vim
  " }}}

  " go {{{
  let g:go_highlight_extra_types = 1
  let g:go_highlight_operators = 1
  let g:go_highlight_functions = 1
  let g:go_highlight_function_parameters = 1
  let g:go_highlight_function_calls = 1
  let g:go_highlight_types = 1
  let g:go_highlight_fields = 1
  let g:go_highlight_variable_declarations = 1
  let g:go_highlight_variable_assignments = 1
  " }}]

" }}}
